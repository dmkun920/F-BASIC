
100 RANDOMIZE TIME/4
110 DEFINT A-Z
120 DIM NAM$(5),X(5),Y(5),L(2)
130 CLS:CONSOLE 0,25,0
140 LOCATE 0,0,1,0,15:COLOR 7,,,0
150 PRINT "何人で遊びますか。（５人以下） :";TAB(79);
160 LOCATE 32,0,1:Z$=INPUT$(1):MEM=VAL(Z$)
170 IF MEM<1 OR MEM>5 THEN 140
180 PRINT Z$:PRINT
190 :
200 FOR I=1 TO MEM
210 X=-30:Y=-30
220 COLOR I+1,,,0
230 PRINT TAB(4);:LINE INPUT "名前 :",Z$
240 NAM$(I)=Z$
250 CIRCLE (X,Y),1,,,,,,PRESET
260 X=INT(RND*640):Y=INT(RND*400)
270 CIRCLE (X,Y),1,I+1,,,,,PSET
280 FOR J=0 TO 100:NEXT
290 IF INKEY$<>" " THEN 250
300 X(I)=X:Y(I)=Y
310 NEXT
320 FOR I=0 TO 3000:NEXT
330 :
340 COLOR=(2,2):DAY=1
350 CLS 4:LOCATE 0,0,0
360 POINT (INT(RND*640),INT(RND*399))
370 FOR I=0 TO 200
380 LINE -(INT(RND*640),INT(RND*399)),PSET,1
390 NEXT
400 :
410 LOCATE 0,0,1:COLOR 7
420 PRINT USING "#####日";DAY:PRINT
430 LIFE=MEM
440 FOR I=1 TO MEM
450 COLOR I+1,,,0
460 PRINT USING "    &            & 君は、";NAM$(I);
470 IF POINT(X(I),Y(I)) THEN 490
480   COLOR 1,,,4:PRINT "生きています。":GOTO 500
490   COLOR 2,,,4:PRINT "死にました。":LIFE=LIFE-1
500 NEXT
510 COLOR 7,,,0:PRINT
520 BEEP 1:FOR I=0 TO 600:NEXT:BEEP 0
530 IF LIFE THEN 590
540 BEEP
550 PRINT "全員死にました。　これで生き残りゲームは終わりです。";
560 IF INKEY$<>"" THEN 560
570 COLOR 1:INPUT "",Z$:GOTO 130
580 :
590 PRINT "続けますか？ (Y/N)";
600 IF INKEY$<>"" THEN 600
610 Z$=INPUT$(1)
620 J=(INSTR("YyﾝNnﾐ",Z$)+2)\3
630 DAY=DAY+1
640 ON J+1 GOTO 610,350,130
650 END
